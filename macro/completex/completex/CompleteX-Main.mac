// 
//  	CompleteX
//  	Version 3.11  (2009/09/10)
//  	Made by IKKI  http://www18.big.or.jp/~fujiwara/ikki/
// 

$CfgFile = currentmacrodirectory + "\\config.txt";
$PrmFile = currentmacrodirectory + "\\system\\listbox.param";

// #debug = 1;
call LoadDLL;
call Main;
call FreeDLL;
endmacro;

Main:
	call GetType;
	if ($Type != "plugin") {
		call GetCfgAsPath "plugin", ".mac";
		if ($$return != "") {
			execmacro $$return;		// プラグインを呼び出す
			return;
		}
	}
	call CompatiBegin;
	call GetCfg "source";  $src = $$return;
	if ($src == "self") {
		execmacro currentmacrodirectory + "\\CompleteWord.mac";
	} else if ($src == "path") {
		execmacro currentmacrodirectory + "\\CompletePath.mac";
	} else if ($src == "font") {
		execmacro currentmacrodirectory + "\\CompleteFont.mac";
	} else if ($src == "list") {
		call GetCfg "list";   $dic  = $$return;
		call GetCfg "hints";  $hint = $$return;
		call Listbox;
	} else {		// デフォルト (auto mode)
		call GetCfgAsPath "dic",  ".dic";   $dic  = $$return;
		call GetCfgAsPath "hint", ".hint";  $hint = $$return;
		if      ($src == "dic") call Listbox;
		else if ($dic == "")    execmacro currentmacrodirectory + "\\CompleteWord.mac";
		else if ($src == "mix") call MixMode;
		else                    call Listbox;
	}
	call CompatiEnd;
	return;

Listbox:		// リストボックス起動
	if ($Type != "plugin") {
		begingroupundo;
	}
	writeinistr $PrmFile, "Parameter", "cfgfile",  $CfgFile;
	writeinistr $PrmFile, "Parameter", "source",   $src;
	writeinistr $PrmFile, "Parameter", "dic",      $dic;
	writeinistr $PrmFile, "Parameter", "hint",     $hint;
	execmacro currentmacrodirectory + "\\system\\listbox.mac";
	#result  = getininum($PrmFile, "Return", "result");
	$instr   = getinistr($PrmFile, "Return", "instr");
	$basestr = getinistr($PrmFile, "Return", "basestr");
	writeinistr $PrmFile, "Parameter", 0, 0;
	if ($Type != "plugin") {
		endgroupundo;
		if (#result == 9 || #result == 13 || #result >= 32) insert char(#result);
		else if (#result <= -1 && $instr == $basestr) undo;
	}
	return;

MixMode:		// 混合補完
	if ($Type != "plugin") {
		begingroupundo;
		writeinistr $CfgFile, "plugin", "type", "plugin";
		writeinistr $CfgFile, "plugin", "@", $Type;
	}
	writeinistr $CfgFile, "plugin", "word.mix", $dic;
	execmacro currentmacrodirectory + "\\CompleteWord.mac";
	#result  = getininum($CfgFile, "plugin", "result");
	$instr   = getinistr($CfgFile, "plugin", "instr");
	$basestr = getinistr($CfgFile, "plugin", "basestr");
	if ($Type != "plugin") {
		writeinistr $CfgFile, "plugin", 0, 0;
		endgroupundo;
		if (#result == 9 || #result == 13 || #result >= 32) insert char(#result);
		else if (#result <= -1 && $instr == $basestr) undo;
	}
	return;

CompatiBegin:		// 後方互換（開始時）
	if ($Type != "plugin") return;
	call GetCfg "withinword";
	if ($$return != "") {
		writeinistr $PrmFile, "Parameter", "wordhas", $$return;
	}
	call GetCfg "autofix";
	if (strstr($$return, "s") >= 0) {
		writeinistr $PrmFile, "Parameter", "autofix", hex(
		 ((strstr($$return, "first")  >= 0) * 0x100) |
		 ((strstr($$return, "last")   >= 0) * 0x001) |
		 ((strstr($$return, "always") >= 0) * 0x111) 
		);
	}
	call GetCfg "path.filesystem";
	if (strstr("Uu", leftstr($$return, 1)) >= 0) {
		writeinistr $CfgFile, "plugin", "path.separator", "/";
	}
	return;

CompatiEnd:		// 後方互換（終了時）
	if ($Type != "plugin") return;
	$instr  = getinistr($PrmFile, "Return", "instr");
	#result = getininum($PrmFile, "Return", "result");
	writeinistr $CfgFile, "plugin", 0, 0;
	writeininum $CfgFile, "plugin", "result", #result;
	if (#result >= 32){
		insert char(#result);
		writeinistr $CfgFile, "plugin", "instr", "\"" + $instr + char(#result) + "\"";
	} else {
		writeinistr $CfgFile, "plugin", "instr", "\"" + $instr + "\"";
	}
	return;

//.------ 共通サブルーチン -----------------------------------------------------

GetType:		// ファイルタイプを取得
	if (getinistr($CfgFile, "plugin", "type") == "plugin") {
		$Type = "plugin";
	} else if (leftstr(dllfuncstr("WINDOW_GETCLASSNAME", hidemaruhandle(0)), 8) == "TuruKame") {
		$Type = "turukame";
	} else {
		$Type = filetype;
	}
	return;

GetCfgAsPath:		// 設定をファイル名として取得
	// $$1 = キー名
	// $$2 = デフォルト拡張子
	call GetCfg $$1;
	$$v = $$return;
	if ($$v == "" && $$2 != "") {
		$$d = currentmacrodirectory + "\\" + midstr($Type, strstr($Type, ".") + 1, 255) + $$2;
		if (existfile($$d)) return $$d;		// ベースフォルダ\拡張子.$$2 があればそれを返す
	}
	if ($$v == "") return "";
	if (midstr($$v, 1, 2) != ":\\" && leftstr($$v, 2) != "\\\\") {
		$$v = leftstr($CfgFile, dllfunc("STRRSTR", $CfgFile, "\\") + 1) + $$v;		// INI ファイルからの相対パス
	}
	if (!existfile($$v)) {
		call Error "指定されたファイルがありません\n"
		 + $$v + "\n\n"
		 + "設定ファイル: " + $CfgFile;
	}
	return $$v;

GetCfg:		// ファイルタイプ別の設定を取得
	// $CfgFile = INI ファイル名
	// $Type = セクション名
	// $$1 = キー名
	$$t = $Type;
	while ($$t != "") {
		$$v = getinistr($CfgFile, $$t, $$1);
		if ($$v != "") break;
		$$t = getinistr($CfgFile, $$t, "@");
	}
	if ($$v == "") $$v = getinistr($CfgFile, "default", $$1);
	return $$v;

LoadDLL:		// DLL ロード
	if (rightstr(loaddllfile, 13) != "\\macrodll.dll") {
		loaddll currentmacrodirectory + "\\system\\macrodll.dll";
		if (!result) {
			call Error "macrodll.dll がロードできません\n"
			 + "CompleteX を再インストールしてみてください\n\n"
			 + $BaseDir + "system\\macrodll.dll";
		}
		if (!dllfunc("SETHMJREDLL", hidemarudir + "\\HmJre." + "dll")) {
			call Error "HmJre." + "dll が見つかりません\n"
			 + "秀丸エディタを再インストールしてみてください\n"
			 + hidemarudir + "\\HmJre." + "dll";
		}
		#basestat = #basestat | 0x01;
	}
	if (#debug) {
		call Confirm "\n" + dllfuncstr("GETVERSIONSTR") + "\n" + dllfuncstr("GETCOMPILEDATE");
	}
	if (dllfunc("GETVERSIONINT") < 8000) {
		call Error "macrodll.dll が古いです\n"
		 + "CompleteX を再インストールしてみてください\n"
		 + "現在のバージョン: " + dllfuncstr("GETVERSIONSTR") + "\n"
		 + "必要なバージョン: 0.80.00";
	}
	return;

FreeDLL:		// DLL 解放
	if (#basestat & 0x01) freedll;
	return;

Error:		// エラー処理
	if ($$1 != "") message currentmacrobasename + ":  " + $$1;
	freedll;
	endmacro;

Confirm:		// デバッグ用
	question currentmacrobasename + ":  " + $$1;
	if (!result) { freedll;  endmacro; }
	return;

//  		(C) IKKI <fujiwara@big.jp> 2006-2009
